extend class HDFollower
{
	enum FOrder
	{
		FOrder_Follow,
		FOrder_FollowIgnore,
		FOrder_Cover,
		FOrder_GoAndCover,
		FOrder_ComeAndHelp
	}

	enum ChangeOrderFlags
	{
		COF_MULTI = 1
	}

	// --------------------------------------------------
	// ACTIONS
	// --------------------------------------------------

	private void HaulAss()
	{
		ClearTarget();
		ClearWeapon();
		SetStateLabel('Idle'); // [Ace] Surely putting this here can't backfire. Right? :uhh:
		Status = FStatus_None;
	}

	bool ChangeOrders(FOrder newOrder, int flags = 0)
	{
		if (Status == FStatus_InactiveOrDead)
		{
			return false;
		}
		
		if (Status == FStatus_WaitingForArmor)
		{
			Status = FStatus_None;
		}

		int Index = Order == newOrder ? 1 : 2;
		switch (newOrder)
		{
			case FOrder_Follow:
				PrintMessage(GetResponse("ORDER_FOLLOW"..Index), "Order/Follow"..Index, PMType_General);
				Order = FOrder_Follow;
				ResetIdleTimer();
				return true;

			case FOrder_FollowIgnore:
				PrintMessage(GetResponse("ORDER_FOLLOWIGNORE"..Index), "Order/FollowIgnore"..Index, PMType_General);
				HaulAss();
				Order = FOrder_FollowIgnore;
				ResetIdleTimer();
				return true;

			case FOrder_Cover:
				PrintMessage(GetResponse("ORDER_COVER"..Index), "Order/Cover"..Index, PMType_General);
				Order = FOrder_Cover;
				ClearGoal();
				return true;

			case FOrder_GoAndCover:
				FLineTraceData TraceData;
				LinkedPlayer.LineTrace(LinkedPlayer.angle, MaxGoalRange * 2, LinkedPlayer.pitch, TRF_THRUACTORS, LinkedPlayer.Height - 8, data: TraceData);
				if ((TraceData.HitType == TraceData.TRACE_HitFloor || TraceData.HitType == TraceData.TRACE_HitWall) && TrySetGoal(TraceData.HitLocation, SGF_IGNOREVALID | (flags & COF_MULTI ? SGF_MULTI : 0)))
				{
					// [Ace] Drop whatever they're doing and haul ass to their destination.
					HaulAss();
					Order = FOrder_GoAndCover;
					ActivateAsPlayer = true;
					PrintMessage(GetResponse("ORDER_GOANDCOVER2"), "Order/GoAndCover2", PMType_General);
					return true;
				}
				else
				{
					PrintMessage(GetResponse("ORDER_GOANDCOVER1"), "Order/GoAndCover1", PMType_Important);
					return false;
				}

			case FOrder_ComeAndHelp:
				if (!PNeedsMedic && !PNeedsHelp)
				{
					PrintMessage(GetResponse("ORDER_HELP3"), "Order/Help3", PMType_Important);
					return true;
				}
				PrintMessage(GetResponse("ORDER_HELP"..Index), "Order/Help"..Index, PMType_Important);
				HaulAss();
				Order = FOrder_ComeAndHelp;
				ResetIdleTimer();
				return true;
		}

		return false;
	}
}