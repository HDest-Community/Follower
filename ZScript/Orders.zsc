enum FOrder
{
	FOrder_Follow,
	FOrder_FollowIgnore,
	FOrder_Cover,
	FOrder_GoAndCover,
	FOrder_ComeAndHelp
}

extend class HDFollower
{
	bool ChangeOrders(FOrder newOrder)
	{
		if (Status == FStatus_InactiveOrDead)
		{
			return false;
		}
		
		if (Status == FStatus_WaitingForArmor)
		{
			Status = FStatus_None;
		}

		string ATag = GetTag();

		int Index = Order == newOrder ? 1 : 2;
		switch (newOrder)
		{
			case FOrder_Follow:
				PrintMessage(GetResponse("$"..ATag.."_ORDER_FOLLOW"..Index), "Follower/"..ATag.."/Order/Follow"..Index, PMType_General);
				Order = FOrder_Follow;
				ResetIdleTimer();
				return true;

			case FOrder_FollowIgnore:
				PrintMessage(GetResponse("$"..ATag.."_ORDER_FOLLOWIGNORE"..Index), "Follower/"..ATag.."/Order/FollowIgnore"..Index, PMType_General);
				HaulAss();
				Order = FOrder_FollowIgnore;
				ResetIdleTimer();
				return true;

			case FOrder_Cover:
				PrintMessage(GetResponse("$"..ATag.."_ORDER_COVER"..Index), "Follower/"..ATag.."/Order/Cover"..Index, PMType_General);
				Order = FOrder_Cover;
				ClearGoal();
				return true;

			case FOrder_GoAndCover:
				FLineTraceData TraceData;
				LinkedPlayer.LineTrace(LinkedPlayer.Angle, MaxGoalRange * 2, LinkedPlayer.Pitch, TRF_THRUACTORS, LinkedPlayer.Height - 8, data: TraceData);
				if ((TraceData.HitType == TraceData.TRACE_HitFloor || TraceData.HitType == TraceData.TRACE_HitWall) && TrySetGoal(TraceData.HitLocation, true))
				{
					// [Ace] Drop whatever they're doing and haul ass to their destination.
					HaulAss();
					Order = FOrder_GoAndCover;
					ActivateAsPlayer = true;
					PrintMessage(GetResponse("$"..ATag.."_ORDER_GOANDCOVER2"), "Follower/"..ATag.."/Order/GoAndCover2", PMType_General);
					return true;
				}
				else
				{
					PrintMessage(GetResponse("$"..ATag.."_ORDER_GOANDCOVER1"), "Follower/"..ATag.."/Order/GoAndCover1", PMType_Important);
					return false;
				}

			case FOrder_ComeAndHelp:
				if (!PlayerNeedsMedic() && !PlayerIsIncapped())
				{
					PrintMessage(GetResponse("$"..ATag.."_ORDER_HELP3"), "Follower/"..ATag.."/Order/Help3", PMType_Important);
					return true;
				}
				PrintMessage(GetResponse("$"..ATag.."_ORDER_HELP"..Index), "Follower/"..ATag.."/Order/Help"..Index, PMType_Important);
				HaulAss();
				Order = FOrder_ComeAndHelp;
				ResetIdleTimer();
				return true;
		}

		return false;
	}

	private void HaulAss()
	{
		ClearTarget();
		ClearWeapon();
		SetStateLabel("Idle"); // [Ace] Surely putting this here can't backfire. Right? :uhh:
		Status = FStatus_None;
	}
}