extend class HDFollower
{
	// --------------------------------------------------
	// ACTIONS
	// --------------------------------------------------

	private void LookForArmor()
	{
		CheckProximity("HDArmour", radius + MaxSearchRange, 1, CPXF_CLOSEST | CPXF_SETTRACER);
		let foundArmor = HDArmour(tracer);
		let wornArmor = HDArmourWorn(FindInventory("HDArmourWorn"));

		if (foundArmor)
		{
			if (!wornArmor)
			{
				PrintMessage(GetResponse("ARMORGIVE_NONE"), "ArmorGive/None", PMType_General);
			}
			else if (wornArmor)
			{
				// [Ace] 1030 because mega armor's durability is calculated as 1000 + actual durability. Only while on the ground, though.
				if (!wornArmor.Mega && foundArmor.Mega && foundArmor.Mags[0] >= 1020 || wornArmor.Mega && wornArmor.Durability < 20 && !foundArmor.Mega && foundArmor.Mags[0] > 60)
				{
					PrintMessage(GetResponse("ARMORGIVE_UPGRADE"), "ArmorGive/Upgrade", PMType_General);
				}
				else if (foundArmor.Mega == wornArmor.Mega && wornArmor.Durability < foundArmor.Mags[0] % 1000)
				{
					PrintMessage(GetResponse("ARMORGIVE_BETTERDURABILITY"), "ArmorGive/BetterDurability", PMType_General);
				}
				else
				{
					PrintMessage(GetResponse("ARMORGIVE_NOTHANKS"), "ArmorGive/NoThanks", PMType_Important);
					Status = FStatus_None;
					tracer = null;
					return;
				}

				A_DropInventory(wornArmor.GetClass());
			}

			foundArmor.ActualPickup(self);
			UseInventory(FindInventory("HDArmour"));
			Status = FStatus_None;
		}

		tracer = null;
	}
	
	// [Ace] Drop the best armor. Don't be a cheapskate.
	// This function only makes follower wait for armor, unless you call it again, in which case they drop their current armor.
	void TryUseArmor()
	{
		if (Status == FStatus_InactiveOrDead)
		{
			return;
		}
		
		let wornArmor = FindInventory("HDArmourWorn");
		if (Status == FStatus_WaitingForArmor)
		{
			if (wornArmor)
			{
				A_DropInventory(wornArmor.GetClass());
				PrintMessage(GetResponse("ARMORGIVE_STRIP"), "ArmorGive/Strip", PMType_General);
				Status = FStatus_None;
				return;
			}
			else
			{
				PrintMessage(GetResponse("ARMORGIVE_CANCEL"), "ArmorGive/Cancel", PMType_Important);
				Status = FStatus_None;
				return;
			}
		}

		Status = FStatus_WaitingForArmor;
		
		string Message = GetResponse("ARMORGIVE_INIT")..(FindInventory("HDArmourWorn") ? " "..GetResponse("ARMORGIVE_MAYBEDROP") : "");
		string VoiceLine = FindInventory("HDArmourWorn") ? "ArmorGive/Init2" : "ArmorGive/Init1";
		PrintMessage(Message, VoiceLine, PMType_Important);
	}
}
