extend class HDFollower
{
	// [Ace] Drop the best armor. Don't be a cheapskate.
	// This function only makes follower wait for armor, unless you call it again, in which case they drop their current armor.
	void TryUseArmor()
	{
		if (Status == FStatus_InactiveOrDead)
		{
			return;
		}
		
		let WornArmor = FindInventory("HDArmourWorn");
		if (Status == FStatus_WaitingForArmor)
		{
			if (WornArmor)
			{
				A_DropInventory(WornArmor.GetClass());
				PrintMessage(GetResponse("$ARMORGIVE_STRIP"), "Follower/ArmorGive/Strip");
				Status = FStatus_None;
				return;
			}
			else
			{
				PrintMessage(GetResponse("$ARMORGIVE_CANCEL"), "Follower/ArmorGive/Cancel");
				Status = FStatus_None;
				return;
			}
		}

		Status = FStatus_WaitingForArmor;

		string Message = GetResponse("$ARMORGIVE_INIT")..(FindInventory("HDArmourWorn") ? " "..GetResponse("$ARMORGIVE_MAYBEDROP") : "");
		string VoiceLine = FindInventory("HDArmourWorn") ? "Follower/ArmorGive/Init2" : "Follower/ArmorGive/Init1";
		PrintMessage(Message, VoiceLine);
	}

	private void LookForArmor()
	{
		CheckProximity("HDArmour", radius + 24, 1, CPXF_CLOSEST | CPXF_SETTRACER);
		let FoundArmor = HDArmour(tracer);
		let WornArmor = HDArmourWorn(FindInventory("HDArmourWorn"));

		if (FoundArmor)
		{
			if (!WornArmor)
			{
				PrintMessage(GetResponse("$ARMORGIVE_NONE"), "Follower/ArmorGive/None");
			}
			else if (WornArmor)
			{
				// [Ace] 1030 because mega armor's durability is calculated as 1000 + actual durability. Only while on the ground, though.
				if (!WornArmor.Mega && FoundArmor.Mega && FoundArmor.Mags[0] >= 1030 || WornArmor.Mega && WornArmor.Durability < 20 && !FoundArmor.Mega && FoundArmor.Mags[0] > 60)
				{
					PrintMessage(GetResponse("$ARMORGIVE_UPGRADE"), "Follower/ArmorGive/Upgrade");
				}
				else if (FoundArmor.Mega == WornArmor.Mega && WornArmor.Durability < FoundArmor.Mags[0] % 1000)
				{
					PrintMessage(GetResponse("$ARMORGIVE_BETTERDURABILITY"), "Follower/ArmorGive/BetterDurability");
				}
				else
				{
					PrintMessage(GetResponse("$ARMORGIVE_NOTHANKS"), "Follower/ArmorGive/NoThanks");
					Status = FStatus_None;
					tracer = null;
					return;
				}

				A_DropInventory(WornArmor.GetClass());
			}

			FoundArmor.ActualPickup(self);
			UseInventory(FindInventory("HDArmour"));
			Status = FStatus_None;
		}

		tracer = null;
	}
}