extend class HDFollower
{
	// [Ace] Drop the best armor. Don't be a cheapskate.
	// This function only makes follower wait for armor, unless you call it again, in which case they drop their current armor.
	void TryUseArmor()
	{
		if (Status == FStatus_InactiveOrDead)
		{
			return;
		}
		
		string ATag = GetTag();
		let WornArmor = FindInventory("HDArmourWorn");
		if (Status == FStatus_WaitingForArmor)
		{
			if (WornArmor)
			{
				A_DropInventory(WornArmor.GetClass());
				PrintMessage(GetResponse("$"..ATag.."_ARMORGIVE_STRIP"), "Follower/"..ATag.."/ArmorGive/Strip", PMType_General);
				Status = FStatus_None;
				return;
			}
			else
			{
				PrintMessage(GetResponse("$"..ATag.."_ARMORGIVE_CANCEL"), "Follower/"..ATag.."/ArmorGive/Cancel", PMType_Important);
				Status = FStatus_None;
				return;
			}
		}

		Status = FStatus_WaitingForArmor;
		
		string Message = GetResponse("$"..ATag.."_ARMORGIVE_INIT")..(FindInventory("HDArmourWorn") ? " "..GetResponse("$"..ATag.."_ARMORGIVE_MAYBEDROP") : "");
		string VoiceLine = FindInventory("HDArmourWorn") ? "Follower/"..ATag.."/ArmorGive/Init2" : "Follower/"..ATag.."/ArmorGive/Init1";
		PrintMessage(Message, VoiceLine, PMType_Important);
	}

	private void LookForArmor()
	{
		CheckProximity("HDArmour", radius + 24, 1, CPXF_CLOSEST | CPXF_SETTRACER);
		let FoundArmor = HDArmour(tracer);
		let WornArmor = HDArmourWorn(FindInventory("HDArmourWorn"));

		if (FoundArmor)
		{
			string ATag = GetTag();
			if (!WornArmor)
			{
				PrintMessage(GetResponse("$"..ATag.."_ARMORGIVE_NONE"), "Follower/"..ATag.."/ArmorGive/None", PMType_General);
			}
			else if (WornArmor)
			{
				// [Ace] 1030 because mega armor's durability is calculated as 1000 + actual durability. Only while on the ground, though.
				if (!WornArmor.Mega && FoundArmor.Mega && FoundArmor.Mags[0] >= 1030 || WornArmor.Mega && WornArmor.Durability < 20 && !FoundArmor.Mega && FoundArmor.Mags[0] > 60)
				{
					PrintMessage(GetResponse("$"..ATag.."_ARMORGIVE_UPGRADE"), "Follower/"..ATag.."/ArmorGive/Upgrade", PMType_General);
				}
				else if (FoundArmor.Mega == WornArmor.Mega && WornArmor.Durability < FoundArmor.Mags[0] % 1000)
				{
					PrintMessage(GetResponse("$"..ATag.."_ARMORGIVE_BETTERDURABILITY"), "Follower/"..ATag.."/ArmorGive/BetterDurability", PMType_General);
				}
				else
				{
					PrintMessage(GetResponse("$"..ATag.."_ARMORGIVE_NOTHANKS"), "Follower/"..ATag.."/ArmorGive/NoThanks", PMType_Important);
					Status = FStatus_None;
					tracer = null;
					return;
				}

				A_DropInventory(WornArmor.GetClass());
			}

			FoundArmor.ActualPickup(self);
			UseInventory(FindInventory("HDArmour"));
			Status = FStatus_None;
		}

		tracer = null;
	}
}