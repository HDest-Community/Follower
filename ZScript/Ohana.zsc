extend class HDFollower
{
	// --------------------------------------------------
	// ACTIONS
	// --------------------------------------------------

	protected void CheckOhana()
	{
		if (!OhanaMode) { OhanaMode = CVar.GetCVar('hdf_ohana'); }

		if (level.MapName ~== "LOTSAGUN")
		{
			return;
		}

		bool nearMerchant = false;
		Name clsStr = 'HDMerchant';
		class<Actor> cls = clsStr;
		if (cls)
		{
			nearMerchant = CheckProximity(cls, HDCONST_ONEMETRE * 10, 1, CPXF_CLOSEST | CPXF_ANCESTOR);
		}

		if (OhanaMode.GetBool() && DistanceToPlayer > HDCONST_ONEMETRE * (CanSeePlayer ? 30 : 10) && (!nearMerchant || GetAge() % 3 == 0))
		{
			OhanaTicker++;
			if (OhanaTicker >= OhanaDeathTime && Health > 0)
			{
				A_Die();
				BodyDamage = 10000;
				BloodReviveTicker = 10000;
				OhanaTicker = 0;
			}
			else if (OhanaTicker >= OhanaDisappearTime)
			{
				Handler.RemoveFollower(self, true);
				return;
			}
		}
		else
		{
			OhanaTicker = 0;
		}
	}

	// --------------------------------------------------
	// CONSTANTS/VARIABLES
	// --------------------------------------------------

	private transient CVar OhanaMode;
	private int OhanaTicker;
	const OhanaDeathTime = 35 * 60 * 5;
	const OhanaDisappearTime = 35 * 60 * 15;
}