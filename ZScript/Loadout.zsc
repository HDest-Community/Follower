extend class HDFollower
{
	virtual void GiveStartingItems()
	{
		Medikits = hdf_loadout_medikits;
		Stimpacks = hdf_loadout_stimpacks + Medikits;

		int ArmorType = hdf_loadout_armor;
		if (ArmorType > 0)
		{
			GiveArmour(1.0, ArmorType >= 2 ? 1.0 : 0.0, 1.0);
		}
	}

	virtual void InitWeapons()
	{
		int WeaponFlags = hdf_loadout_weapons;
		int StartingAmmoFlags = hdf_loadout_startingammo;

		static const string AllWeapons[] = { "FW_SMG", "FW_Hunter", "FW_ZM66", "FW_RocketLauncher", "FW_Liberator", "FW_Boss", "FW_Bronto" };
		for (int i = 0; i < AllWeapons.Size(); ++i)
		{
			class<FollowerWeapon> fw = AllWeapons[i];
			if (fw && WeaponFlags & (1 << i))
			{
				FollowerWeapon wpn = FollowerWeapon(new(fw));
				if (StartingAmmoFlags & (1 << i))
				{
					wpn.Init();
				}
				Weapons.Push(wpn);
			}
		}
	}
}