extend class HDFollower
{
	// --------------------------------------------------
	// VIRTUALS
	// --------------------------------------------------
	
	virtual void GiveStartingItems(string input = "")
	{
		input = input.MakeLower();
		input.Replace(" ", "");

		Array<string> lines;
		input.Split(lines, ",");

		for (int i = 0; i < lines.Size(); ++i)
		{
			double amt = lines[i].Mid(3).ToDouble();
			if (amt == 0)
			{
				amt = 1;
			}
			string ref = lines[i].Left(3);

			class<Inventory> cls = HDBackpack.FindByRefId(ref);
			if (!cls)
			{
				continue;
			}

			if (cls is 'BattleArmour' || cls is 'BattleArmourWorn')
			{
				bool mega = cls == 'BattleArmour' || cls == 'BattleArmourWorn';
				GiveArmour(1.0, mega ? 1.0 : 0, amt);
				continue;
			}

			if (cls is 'HDWeapon')
			{
				for (int j = 0; j < Handler.AllWeapons.Size(); ++j)
				{
					let tempWpn = FollowerWeapon(new(Handler.AllWeapons[j]));
					class<HDWeapon> realWpn = tempWpn.GetRealWeapon();
					if (realWpn && realWpn == cls)
					{
						FollowerWeapon.CreateAndAdd(tempWpn.GetClass(), self);
						break;
					}
				}
			}

			class<HDPickup> pkp = (class<HDPickup>)(cls);
			if (IsValidItem(pkp, true))
			{
				if (pkp is 'HDMagAmmo')
				{
					for (int j = 0; j < int(amt); ++j)
					{
						HDMagAmmo spawned = HDMagAmmo(Spawn(pkp, pos));
						spawned.SyncAmount();
						if (!AddMag(spawned, flags: FIF_FORCE))
						{
							spawned.Destroy();
							return;
						}
					}
				}
				else
				{
					HDPickup spawned = HDPickup(Spawn(pkp, pos));
					spawned.Amount = int(amt);
					if (!AddItem(spawned, int(amt), spawned.bMULTIPICKUP ? MaxLooseAmmoAmount : 1, flags: FIF_FORCE))
					{
						spawned.Destroy();
						break;
					}
				}
			}
		}
	}
}