extend class HDFollower
{
	private bool TrySetGoal(vector3 spawnPos, bool force = false)
	{
		Actor tg = Spawn('TargetGoal', spawnPos);
		if (tg && tg.pos.z - tg.floorz <= MaxStepHeight - 8 && Distance2D(tg) < MaxGoalRange && CheckSight(tg) && (force || tg.TestMobjLocation() && level.IsPointInLevel(spawnPos) && !CheckProximity('HDFollower', 64, 1, CPXF_ANCESTOR)))
		{
			ClearGoal(); // [Ace] Nuke old goal.
			TGoal = tg;
			return true;
		}
		else
		{
			tg.Destroy();
			return false;
		}
	}

	private void ClearGoal(bool keepLastPos = false)
	{
		if (TGoal is 'TargetGoal')
		{
			TGoal.Destroy();
		}
		TGoal = null; // [Ace] Just in case it's the player.

		if (!keepLastPos)
		{
			LastKnownPos = (0, 0, 0);
		}
	}

	const MaxGoalRange = 2048;
}

class TargetGoal : Actor
{
	Default
	{
		Radius 12;
		Height 52;
		+NOINTERACTION
	}

	States
	{
		Spawn:
			TNT1 A 35 * 15;
			Stop;
	}
}