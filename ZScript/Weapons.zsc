class FollowerWeapon
{
	enum CRType
	{
		CRType_Empty,
		CRType_Chambered,
		CRType_Spent
	}

	CRType ChamberedRound;
	int Mag;
	int SpareAmmo;
	bool HasWarnedLowAmmo;

	virtual void Init() { }
	virtual string GetName() { return "N/A"; } // [Ace] Name is used for LANGUAGE identification.
	virtual class<HDAmmo> GetAmmoType() { return null; }
	virtual string GetAmmoName() { return "N/A"; }
	virtual StateLabel GetFireState() { return null; }
	virtual StateLabel GetReloadState() { return null; }
	virtual int GetMaxMag() { return 0; } // [Ace] Max mag being 0 means that spare ammo is treated as mag.
	virtual int GetMaxSpare() { return 0; }
	virtual double GetAdjustmentMult() { return 1.0; }

	clearscope int GetLoadedRounds()
	{
		return Mag + (ChamberedRound == CRType_Chambered);
	}

	bool TryFireRound(bool eject)
	{
		if (ChamberedRound == CRType_Chambered)
		{
			ChamberedRound = eject ? CRType_Empty : CRType_Spent;
			return true;
		}

		return false;
	}

	virtual bool TryChamberRound()
	{
		if (ChamberedRound == CRType_Empty && Mag > 0)
		{
			Mag--;
			ChamberedRound = CRType_Chambered;
			return true;
		}

		return false;
	}
	
	virtual bool TryReloadMag()
	{
		if (SpareAmmo <= 0)
		{
			return false;
		}

		int AmmoMissing = GetMaxMag() - Mag;
		int AmmoToLoad = min(AmmoMissing, SpareAmmo);
		SpareAmmo -= AmmoToLoad;
		Mag += AmmoToLoad;

		return true;
	}
}

class FW_ZM66 : FollowerWeapon
{
	override void Init()
	{
		ChamberedRound = CRType_Chambered;
		Mag = 50;
	}
	override string GetName() { return "ZM66"; }
	override class<HDAmmo> GetAmmoType() { return "FourMilAmmo"; }
	override string GetAmmoName() { return "4mm Rounds"; }
	override int GetMaxMag() { return 50; }
	override int GetMaxSpare() { return 250; }
	override StateLabel GetFireState() { return "ShootZM66"; }
	override StateLabel GetReloadState() { return "ReloadZM66"; }
}

class FW_Boss : FollowerWeapon
{
	override string GetName() { return "Boss"; }
	override class<HDAmmo> GetAmmoType() { return "SevenMilAmmo"; }
	override string GetAmmoName() { return "7mm Rounds"; }
	override int GetMaxMag() { return 10; }
	override int GetMaxSpare() { return 50; }
	override StateLabel GetFireState() { return "ShootBoss"; }
	override StateLabel GetReloadState() { return "ReloadBoss"; }

	override bool TryReloadMag()
	{
		if (SpareAmmo <= 0 || Mag == GetMaxMag())
		{
			return false;
		}

		SpareAmmo -= 1;
		Mag += 1;
		
		return true;
	}
}

class FW_Bronto : FollowerWeapon
{
	override string GetName() { return "Bronto"; }
	override class<HDAmmo> GetAmmoType() { return "BrontornisRound"; }
	override string GetAmmoName() { return "Brontornis Bolt"; }
	override int GetMaxMag() { return 0; }
	override int GetMaxSpare() { return 5; }
	override StateLabel GetFireState() { return "ShootBronto"; }
	override StateLabel GetReloadState() { return "ReloadBronto"; }

	override bool TryChamberRound()
	{
		if (ChamberedRound == CRType_Empty && SpareAmmo > 0)
		{
			SpareAmmo--;
			ChamberedRound = CRType_Chambered;
			return true;
		}

		return false;
	}
}

extend class HDFollower
{
	enum CSFlags
	{
		CSF_FIRE	= 1 << 0,
		CSF_RELOAD	= 1 << 1,
		CSF_ALL		= CSF_FIRE | CSF_RELOAD
	}

	enum RLFlags
	{
		RLF_FORCE	= 1 << 0,
		RLF_MANUAL	= 1 << 1,
		RLF_SILENT	= 1 << 2,
		RLF_NOJUMP	= 1 << 3
	}

	private int AdjustForDistance(double distance, double mult = 1.0) const
	{
		return int(clamp((distance / 64.0) * mult, 4, 80));
	}

	clearscope int GetMaxGiveAmount(FollowerWeapon wpn)
	{
		int MaxMag = wpn.GetMaxMag();
		return min(LinkedPlayer.CountInv(wpn.GetAmmoType()), (MaxMag > 0 ? MaxMag : 1), wpn.GetMaxSpare() - wpn.SpareAmmo);
	}

	protected void Aim()
	{
		A_Face(CTarget, 0, 0, flags: FAF_MIDDLE);
		
		// [Ace] If you're incapped, follower can probably shoot over you. Otherwise it's not a good idea.
		Actor ATarget = AimTarget();
		if (ATarget && bFRIENDLY && (ATarget is "PlayerPawn" || ATarget.bFRIENDLY))
		{
			AimTimer = 0;
			SetStateLabel("Aiming");
		}
	}

	clearscope FollowerWeapon FindWeapon(class<FollowerWeapon> wpn)
	{
		for (int i = 0; i < Weapons.Size(); ++i)
		{
			if (Weapons[i].GetClass() == wpn)
			{
				return Weapons[i];
			}
		}

		return null;
	}

	clearscope FollowerWeapon GetWeaponByIndex(int index)
	{
		int Size = Weapons.Size();
		if (index > -1 && Size > 0 && index < Size)
		{
			return Weapons[index];
		}

		return null;
	}

	// [Ace] It's an int because it's used in NetworkProcess.
	void TryGiveAmmo(int wpnIndex)
	{
		FollowerWeapon wpn = GetWeaponByIndex(wpnIndex);
		if (Status == FStatus_InactiveOrDead || !wpn)
		{
			return;
		}

		class<Inventory> AmmoType = wpn.GetAmmoType();
		int MaxGive = GetMaxGiveAmount(wpn);

		if (Distance3D(LinkedPlayer) > radius + MaxInventoryDistance)
		{
			TryReloadWeapon(wpn, wpn.GetMaxMag(), RLF_MANUAL);
			ThinkMessage(LinkedPlayer, "$AMMOGIVE_FAR_PLR");
			return;
		}
		else if (wpn.SpareAmmo >= wpn.GetMaxSpare())
		{
			TryReloadWeapon(wpn, wpn.GetMaxMag(), RLF_MANUAL);
			PrintMessage(GetResponse("$AMMOGIVE_"..wpn.GetName().."_TOOMANY"), "Follower/AmmoGive/"..wpn.GetName().."/TooMany");
			return;
		}
		else if (LinkedPlayer.CountInv(AmmoType) == 0)
		{
			TryReloadWeapon(wpn, wpn.GetMaxMag(), RLF_MANUAL);
			ThinkMessage(LinkedPlayer, "$AMMOGIVE_NOTENOUGH_PLR");
			return;
		}

		LinkedPlayer.A_TakeInventory(AmmoType, MaxGive);
		wpn.SpareAmmo += MaxGive;
		wpn.HasWarnedLowAmmo = false;
		TryReloadWeapon(wpn, wpn.GetMaxMag(), RLF_MANUAL);

		PrintMessage(GetResponse("$AMMOGIVE_"..wpn.GetName().."_SUCCESS"), "Follower/AmmoGive/"..wpn.GetName().."/Success");
	}

	// [Ace] If weapon is out of ammo, reload.
	protected void TryReloadWeapon(FollowerWeapon wpn, int min = 0, RLFlags flags = 0)
	{
		if (SelWeapon && !(flags & RLF_FORCE))
		{
			return;
		}

		int MaxMag = wpn.GetMaxMag();
		if (MaxMag > 0 && (wpn.Mag <= 0 || wpn.Mag < min) || MaxMag == 0 && wpn.ChamberedRound != FollowerWeapon.CRType_Chambered)
		{
			if (wpn.SpareAmmo > 0)
			{
				// Console.Printf("Trying to reload "..wpn.GetName()..".");
				SelWeapon = wpn;
				Status = FStatus_InCombat;
				SetStateLabel(wpn.GetReloadState());
			}
			else
			{
				// [Ace] Keep complaining if manual.
				if (!(flags & RLF_SILENT) && (!wpn.HasWarnedLowAmmo || (flags & RLF_MANUAL)))
				{
					wpn.HasWarnedLowAmmo = true;
					PrintMessage(GetResponse("$NOAMMO_"..wpn.GetName()), "Follower/NoAmmo/"..wpn.GetName());
				}

				if (!(flags & RLF_NOJUMP))
				{
					ClearWeapon();
					SetStateLabel("Idle");
				}
			}
		}
	}

	protected void EngageTarget(Array<class<FollowerWeapon> > wpns, Actor other)
	{
		// [Ace] This is used so that FindWeapon() isn't copy-pasted numerous times in the targeting code.
		Array<FollowerWeapon> ActualWeapons;
		for (int i = 0; i < wpns.Size(); ++i)
		{
			ActualWeapons.Push(FindWeapon(wpns[i]));
		}

		FollowerWeapon HighestWithSpare = null;
		FollowerWeapon HighestWithAmmo = null;
		SelWeapon = null;

		// [Ace] Find a weapon that has ammo in it. Start from the end and work backwards.
		for (int i = ActualWeapons.Size() - 1; i >= 0; --i)
		{
			int MaxMag = ActualWeapons[i].GetMaxMag();
			if (ActualWeapons[i].SpareAmmo > 0)
			{
				HighestWithSpare = ActualWeapons[i];
			}
			if (ActualWeapons[i].Mag > 0 || ActualWeapons[i].ChamberedRound == FollowerWeapon.CRType_Chambered)
			{
				HighestWithAmmo = ActualWeapons[i];
			}
		}

		if (HighestWithAmmo)
		{
			SelWeapon = HighestWithAmmo;
		}
		else if (HighestWithSpare)
		{
			SelWeapon = HighestWithSpare;
			TryReloadWeapon(SelWeapon, 0, RLF_SILENT | RLF_NOJUMP);
			Status = FStatus_InCombat;
			return;
		}
		
		if (other && SelWeapon)
		{
			CTarget = other;
			SetStateLabel("See");
		}
	}

	virtual void InitWeapons()
	{
		FollowerWeapon wpn;
		wpn = new("FW_ZM66"); wpn.Init(); Weapons.Push(wpn);
		wpn = new("FW_Boss"); wpn.Init(); Weapons.Push(wpn);
		wpn = new("FW_Bronto"); wpn.Init(); Weapons.Push(wpn);
	}

	private void ClearWeapon()
	{
		SelWeapon = null;
	}

	protected FollowerWeapon SelWeapon;
	Array<FollowerWeapon> Weapons;

	private int AimTimer;
	
	States
	{
		See:
			#### FF 2
			{
				Status = FStatus_InCombat;
				AimTimer = 0;
			}
		Aiming:
			#### F 1; // [Ace] Otherwise it's gonna cause an infinite loop.
			#### F 1
			{
				if (!CheckTarget())
				{
					SetStateLabel("Idle");
					Status = FStatus_None;
					return;
				}

				Aim();
				if (AimTimer++ > AdjustForDistance(Distance3D(CTarget), SelWeapon.GetAdjustmentMult()))
				{
					AimTimer = 0;
					SetStateLabel("Shoot");
				}
			}
			Loop;
		Shoot:
			#### F 0
			{
				if (SelWeapon)
				{
					// Console.Printf("Trying to fire "..SelWeapon.GetName().." at "..CTarget.GetTag()..".");
					SetStateLabel(SelWeapon.GetFireState());
					return;
				}
			}
			Goto Idle;

		// ----------------------------------------
		// ZM66
		// ----------------------------------------

		ShootZM66:
			#### GGG 1
			{
				// [Ace] Technically it's caseless so it doesn't eject anything but for the sake of code let's assume it does.
				if (SelWeapon.TryFireRound(true))
				{
					A_StartSound("weapons/rifle", CHAN_WEAPON);
					HDBulletActor.FireBullet(self, "HDB_426");
					A_AlertMonsters(flags: AMF_TARGETEMITTER);
					SelWeapon.TryChamberRound();
				}

				TryReloadWeapon(SelWeapon, flags: RLF_FORCE);
			}
			#### F 10;
			Goto See;
		ReloadZM66:
			#### F 8
			{
				bNOPAIN = true;
			}
			#### F 6
			{
				A_StartSound("weapons/rifleclick2");
			}
			#### F 0
			{
				A_StartSound("weapons/rifleload");
				HDMagAmmo.SpawnMag(self, "HD4mMag", 0);
			}
			#### F 30 A_StartSound("weapons/pocket", 8);
			#### F 10
			{
				A_StartSound("weapons/rifleload", 9);
				SelWeapon.TryReloadMag();
			}
			#### F 0 A_JumpIf(!SelWeapon.TryChamberRound(), 2);
			#### F 2 A_StartSound("weapons/rifleclick2", 8);
			#### F 0
			{
				bNOPAIN = false;
			}
			Goto See;

		// ----------------------------------------
		// BOSS RIFLE
		// ----------------------------------------

		ShootBoss:
			#### F 0
			{
				if (!CheckTarget())
				{
					SetStateLabel("See");
					return;
				}
			}
			#### F 4 Aim();
			#### G 1
			{
				if (SelWeapon.TryFireRound(false))
				{
					A_StartSound("weapons/bigrifle2", CHAN_WEAPON);
					HDBulletActor.FireBullet(self, "HDB_776", speedfactor: 0.99);
					A_AlertMonsters(flags: AMF_TARGETEMITTER);
				}

				TryReloadWeapon(SelWeapon, flags: RLF_FORCE);
			}
			#### F 5;
		ChamberRound:
			#### F 0
			{
				bNOPAIN = true;
			}
			#### F 5 A_StartSound("weapons/boltback", 8);
			#### F 7
			{
				if (SelWeapon.ChamberedRound == FollowerWeapon.CRType_Spent)
				{
					A_SpawnItemEx("HDSpent7mm", cos(pitch) * 8, 1, height - 7 - sin(pitch) * 8, cos(pitch) * cos(angle - 80) * 6 + vel.x, cos(pitch) * sin(angle - 80) * 6 + vel.y, -sin(pitch) * 6+ vel.z, 0, SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH);
					SelWeapon.ChamberedRound = FollowerWeapon.CRType_Empty;
				}
				SelWeapon.TryChamberRound();
				A_StartSound("weapons/boltfwd", 8);
			}
			#### F 0
			{
				bNOPAIN = false;
			}
			Goto ShootBoss;
		ReloadBoss:
			#### F 8
			{
				bNOPAIN = true;
			}
			#### F 2 A_StartSound("weapons/rifleclick2", 8, CHANF_OVERLAP, 0.9, pitch: 0.95);
			#### F 4 A_StartSound("weapons/rifleload", 8, CHANF_OVERLAP);
			#### F 20 A_StartSound("weapons/pocket", 8);
		ReloadBossLoop:
			#### F 12
			{
				if (SelWeapon.TryReloadMag())
				{
					A_StartSound("weapons/rifleclick2", 8);
				}
				else
				{
					if (SelWeapon.ChamberedRound == FollowerWeapon.CRType_Chambered)
					{
						SetStateLabel("See");
					}
					else
					{
						SetStateLabel("ChamberRound");
					}
					bNOPAIN = false;
				}
			}
			Loop;

		// ----------------------------------------
		// BRONTORNIS
		// ----------------------------------------

		ShootBronto:
			#### G 1
			{
				if (SelWeapon.TryFireRound(false))
				{
					A_StartSound("weapons/bronto",CHAN_WEAPON);
					A_StartSound("weapons/bronto",CHAN_WEAPON, CHANF_OVERLAP);
					A_StartSound("weapons/bronto2",CHAN_WEAPON, CHANF_OVERLAP);
					HDBulletActor.FireBullet(self, "HDB_bronto");
					A_AlertMonsters(flags: AMF_TARGETEMITTER);
					A_Recoil(6);
				}
				
				TryReloadWeapon(SelWeapon, flags: RLF_FORCE);
			}
			#### F 10;
			Goto See;
		ReloadBronto:
			#### F 8
			{
				bNOPAIN = true;
			}
			#### F 5
			{
				A_StartSound("weapons/brontunload", 8);
				if (SelWeapon.ChamberedRound == FollowerWeapon.CRType_Spent)
				{
					A_SpawnItemEx("TerrorCasing", cos(pitch) * 4, 0, height - 10 - sin(pitch) * 4, vel.x, vel.y, vel.z - frandom(-1, 1), frandom(-1, 1), SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION);
					SelWeapon.ChamberedRound = FollowerWeapon.CRType_Empty;
				}
			}
			#### F 12 A_StartSound("weapons/brontoload", 9);
			#### F 12 A_StartSound("weapons/pocket", 10);
			#### F 10
			{
				A_StartSound("weapons/brontoload", 10);
				SelWeapon.TryChamberRound();
			}
			#### F 10 A_StartSound("weapons/brontunload", 11);
			#### F 5 A_StartSound("weapons/brontoclose", 8);
			#### F 0
			{
				bNOPAIN = false;
			}
			Goto See;
	}
}