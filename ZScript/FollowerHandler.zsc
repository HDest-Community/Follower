class FollowerSelection
{
	transient Array<HDFollower> SelFollowers;
	clearscope HDFollower GetLast()
	{
		int Size = SelFollowers.Size();
		return Size > 0 ? SelFollowers[Size - 1] : null;
	}
}

class FollowerHandler : EventHandler
{
	enum OperationModifiers
	{
		OpMod_Shift = 1,
		OpMod_Ctrl = 2,
		OpMod_Alt = 4
	}

	// ----------------------------------------------------------
	// CORE
	// ----------------------------------------------------------

	private transient bool CanExitNow;
	private transient int WarningResetTimer;

	private void InitSelection()
	{
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			if (!Selection[i])
			{
				Selection[i] = new('FollowerSelection');
			}
		}
	}

	override void WorldLoaded(WorldEvent e)
	{
		if (GameState == GS_TITLELEVEL)
		{
			return;
		}

		InitSelection();

		let Arbitrator = players[Net_Arbitrator].mo;
		if (!Arbitrator)
		{
			return;
		}

		Array<class<HDFollower> > FollowerClasses;
		for (int i = 0; i < AllActorClasses.Size(); ++i)
		{
			if (AllActorClasses[i] is 'HDFollower' && AllActorClasses[i].GetClassName() != 'HDFollower')
			{
				FollowerClasses.Push((class<HDFollower>)(AllActorClasses[i]));
			}
		}

		int FollowerSpawns = hdf_followers;
		Array<FollowerBody> InvFollowers;
		FetchFollowersInInventory(InvFollowers);
		for (int i = 0; i < FollowerClasses.Size(); ++i)
		{
			// [Ace] FollowerSpawns only affects built-in followers.
			if (FollowerSpawns & (1 << i) || i > InternalFollowers - 1)
			{
				int Index = -1;
				for (int j = 0; j < InvFollowers.Size(); ++j)
				{
					if (InvFollowers[j].Data[InvFollowers[j].Data.Size() - 1].FollowerClass == FollowerClasses[i])
					{
						Index = j;
						break;
					}
				}
				
				// [Ace] Only attempt to spawn a follower if they haven't been picked up by any of the other players.
				if (Index == -1)
				{
					if (GetDefaultByType(FollowerClasses[i]).MissingClass)
					{
						CreateFollower(FollowerClasses[i], Net_Arbitrator);
					}
				}
				else if (CVar.GetCVar('hdf_droponstart', InvFollowers[Index].owner.player).GetBool())
				{
					for (int j = 0; j < 50 && InvFollowers[Index]; ++j)
					{
						InvFollowers[Index].owner.UseInventory(InvFollowers[Index]);
					}
					InvFollowers.Delete(Index);
				}
			}
		}
	}

	private void FetchFollowersInInventory(out Array<FollowerBody> arr)
	{
		arr.Clear();
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			let plr = players[i].mo;
			if (!plr)
			{
				continue;
			}

			Inventory Next = plr.Inv;
			while (Next)
			{
				if (Next is 'FollowerBody')
				{
					arr.Push(FollowerBody(Next));
				}
				Next = Next.Inv;
			}
		}
	}

	override void WorldUnloaded(WorldEvent e)
	{
		while (Followers.Size() > 0)
		{
			HDFollower flw = Followers[0];
			if (flw.IsDeadOrMissing())
			{
				RemoveFollower(flw, true);
			}
			else
			{
				flw.PackFollower(true);
			}
		}
	}

	override void WorldLinePreActivated(WorldEvent e)
	{
		// [Ace] This only prints the warning. Tokens are given when you actually exit the level.
		if (e.Thing is "PlayerPawn" && (e.ActivatedLine.special == 74 || e.ActivatedLine.special == 243 || e.ActivatedLine.special == 244) && !CanExitNow)
		{
			Array<HDFollower> MissingFollowers;
			for (int i = 0; i < Followers.Size(); ++i)
			{
				// [Ace] Ignore expendable followers.
				if (Followers[i].IsDeadOrMissing() && Followers[i].MissingClass)
				{
					MissingFollowers.Push(Followers[i]);
				}
			}
			
			int MSize = MissingFollowers.Size();
			if (MSize > 0)
			{
				// [Ace] Gotta have that proper grammar.
				string FNames = "";
				for (int i = 0; i < MSize; ++i)
				{
					FNames = FNames..MissingFollowers[i].GetTag();

					if (i + 2 == MSize)
					{
						FNames = FNames..(MSize > 2 ? "," : "").." and ";
					}
					else if (i + 2 < MSize)
					{
						FNames = FNames..", ";
					}
				}

				e.Thing.A_Print("\c[White]You shouldn't leave "..FNames.." behind.\c-\n\n\c[White]Activate the exit again within two seconds to exit the level and leave "..(MSize > 1 ? "them" : MissingFollowers[0].GetPronoun(false)).." to the monsters.\c-", 4);
				e.ShouldActivate = false;
				CanExitNow = true;
				WarningResetTimer = 70;
			}	
		}
	}

	override void WorldTick()
	{
		if (CanExitNow && --WarningResetTimer <= 0)
		{
			CanExitNow = false;
		}

		if (FollowerPage[consoleplayer] > 0 && FollowerMenuLevel[consoleplayer] == FMenu_Closed)
		{
			FollowerPage[consoleplayer] = 0;
		}
	}

	// ----------------------------------------------------------
	// INFORMATION
	// ----------------------------------------------------------

	int FollowingFollowersCount()
	{
		int Total = 0;
		for (int i = 0; i < Followers.Size(); ++i)
		{
			if (Followers[i].Health > 0 && (Followers[i].Order == FOrder_Follow || Followers[i].Order == FOrder_FollowIgnore))
			{
				Total++;
			}
		}
		return Total;
	}
}