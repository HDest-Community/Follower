enum FStatus
{
	FStatus_None,
	FStatus_HealingSelf,
	FStatus_HealingPlayer,
	FStatus_WaitingForArmor,
	FStatus_WaitingForWeapon,
	FStatus_InactiveOrDead
}

extend class HDFollower
{
	enum BlockedSide
	{
		BSide_Left = 1,
		BSide_Right = 2
	}

	// --------------------------------------------------
	// ACTIONS
	// --------------------------------------------------

	protected void AiThink()
	{
		if (Status == FStatus_InactiveOrDead || InStateSequence(CurState, FindState("Spawn"))) // [Ace] I don't remember why the Spawn state check...
		{
			return;
		}

		int blockedSides = GetBlockedSides();

		Actor next = GetNextInChain();
		bool canSeeNext = CheckSight(next, SF_ALL);
		if (!canSeeNext && (!TGoal || (blockedSides & (BSide_Left | BSide_Right))) && CanSeePlayer)
		{
			next = LinkedPlayer;
			canSeeNext = true;
		}

		if (hdf_debug & FDebug_Status)
		{
			Console.Printf("[%s] %s (index %i) has %s out with status %i.\nCurrent status is %i. Current order is %i. Follower is %smoving. Goal is %s.\nFollower is targeting %s. Follower is following %s. Commander is %s.\n--------------------",
				GetTimestamp(),
				GetTag(),
				Index,
				SelWeapon ? SelWeapon.GetName() : "no weapon",
				SelWeapon ? SelWeapon.Status : -1,
				Status, Order, !Moving ? "not " : "",
				TGoal ? TGoal.GetTag() : "nothing",
				FTarget.Actor ? FTarget.Actor.GetTag() : "nothing",
				next ? next.GetTag() : "nothing",
				LinkedPlayer ? LinkedPlayer.GetTag() : "nobody");
		}
		
		PNeedsHelp = PlayerIsIncapped(); // [Ace] You're just incapped.
		PNeedsMedic = PlayerNeedsMedic(); // [Ace] You're bleeding and incapped.
		int ffcount = Handler.FollowingFollowersCount();
		int findex = Handler.Followers.Find(self);
		double distDiv = next != LinkedPlayer ? 1.0 + 0.26 * (min(4, ffcount - 1)) : 1.0;
		
		if (!SelWeapon)
		{
			if (Order == FOrder_HealSelf && !Moving && Status != FStatus_HealingSelf)
			{
				Status = FStatus_HealingSelf;
				SetStateLabel('StapleOwnAss');
			}

			if (Status >= FStatus_WaitingForArmor && Status <= FStatus_WaitingForWeapon)
			{
				if (Distance3D(LinkedPlayer) > MaxWaitRange)
				{
					switch (Status)
					{
						case FStatus_WaitingForArmor: PrintMessage(GetResponse("ARMORGIVE_RANAWAY"), "ArmorGive/RanAway", PMType_Critical); break;
						case FStatus_WaitingForWeapon: PrintMessage(GetResponse("WEAPONGIVE_RANAWAY"), "WeaponGive/RanAway", PMType_Critical); break;
					}
					Status = FStatus_None;
				}
				else
				{
					switch (Status)
					{
						case FStatus_WaitingForArmor: LookForArmor(); break;
						case FStatus_WaitingForWeapon: LookForWeapon(); break;
					}
					A_Face(LinkedPlayer, 15);
				}
			}
		}

		if (Status == FStatus_None)
		{
			if (!SelWeapon || SelWeapon.Status < FollowerWeapon.WStatus_Firing)
			{
				bNOPAIN = false;

				if (!Moving && !IsInIdleState())
				{
					SetStateLabel('Idle');
				}
				else if (Moving && !IsInMovingState())
				{
					SetStateLabel('Moving');
				}
			}

			if (Order == FOrder_ComeAndHelp && bFRIENDLY && DistanceToPlayer < 512 && CanSeePlayer)
			{
				if (!PNeedsMedic && !PNeedsHelp)
				{
					StopHealing();	
				}
				else if (DistanceToPlayer < MedicRange && Status != FStatus_HealingPlayer)
				{
					Status = FStatus_HealingPlayer;
					if (PNeedsMedic)
					{
						SetStateLabel('StapleHumptyDumptyBackTogetherAgain');
					}
					else if (PNeedsHelp)
					{
						SetStateLabel('PullYouUp');
					}
				}
				else
				{
					TGoal = LinkedPlayer;
				}
			}
			else if (Order == FOrder_Follow)
			{
				if (canSeeNext)
				{
					LastKnownPos = next.Pos;
				}

				// [Ace] Go to goal if it's too far away.
				// !bSTANDSTILL means the follower will only chase goals if its current goal isn't forced.
				double DistanceToNext = Distance3D(next);
				if (bFRIENDLY && (DistanceToNext > GetMinRange(TGoal, distDiv) || !canSeeNext) && DistanceToNext < MaxFollowRange && (!TGoal || TGoal && !TGoal.bSTANDSTILL))
				{
					if (canSeeNext)
					{
						TGoal = next;
					}
					// [Ace] If following the goal and the goal is nearby but suddenly goes around a corner, try to go around the same corner.
					else if (LastKnownPos != (0, 0, 0) && (!TGoal || TGoal && TGoal.pos != LastKnownPos))
					{
						TrySetGoal(LastKnownPos, SGF_IGNOREVALID | SGF_LASTKNOWN);
					}
				}

				if (!Moving && IsSteppingOnSomeone())
				{
					// [Ace] Spawn a new idle spot.
					for (int i = 0; i < 100; ++i)
					{
						vector3 SpotPos = Vec3Angle(frandom(radius * 3, radius * 6), random(0, 359));
						if (TrySetGoal(SpotPos))
						{
							break;
						}
					}
				}
			}
			else if (Order == FOrder_Stay && DistanceToPlayer < HDCONST_ONEMETRE * 5)
			{
				A_Face(LinkedPlayer, 15);
			}

			if (hdf_collectbrass)
			{
				VacuumBrass();
			}

			// [Ace] Using potions.
			if ((!SelWeapon || SelWeapon.Status == FollowerWeapon.WStatus_Idle) && Order <= FOrder_Stay && !Moving && CanHealSelf() && GetAmount('BluePotion') > 0 && GetAge() % 24 == 0)
			{
				let potion = FindLowestAmount('BluePotion');
				potion.Amount--;

				int toHeal = default.Health / 4;
				HealSelf(toHeal);
				A_StartSound("potion/chug", 5);

				if (potion.Amount == 0)
				{
					TakeInvItem(potion, FIF_FORCE | FIF_REMOVE);
					A_StartSound("potion/open", 6);

					Actor a = Spawn("SpentBottle", pos + (0, 0, height- 4), ALLOW_REPLACE);
					a.angle = angle + 2;
					a.vel = vel;
					a.A_ChangeVelocity(3, 1, 4, CVF_RELATIVE);
					
					a = Spawn("SpentCork", pos + (0, 0, height - 4) , ALLOW_REPLACE);
					a.angle = angle + 3;
					a.vel = vel;
					a.A_ChangeVelocity(5, 3, 4, CVF_RELATIVE);
				}
			}

			// [Ace] Walk away if you get too close.
			if ((!Moving || TGoal && TGoal.bSTANDSTILL) && DistanceToPlayer <= HDCONST_ONEMETRE / 2 + radius + LinkedPlayer.radius && GetPlayerMovementAngle() <= MaxMovementAngle && LinkedPlayer.vel.xy.length() > 0.25 && LinkedPlayer.player.cmd.buttons & BT_SPEED)
			{
				TrySetGoal(Vec3Angle(HDCONST_ONEMETRE * 1.5, AngleTo(LinkedPlayer) + 180), SGF_IGNOREVALID | SGF_MOVEAWAY);
			}

			// [Ace] Mutually put out any fires.
			for (int i = 0; i < Handler.Followers.Size(); ++i)
			{
				if (Handler.Followers[i] != self && self.Distance3D(Handler.Followers[i]) <= HDCONST_ONEMETRE)
				{
					A_GiveInventory('HDFireDouse', 3);
					Handler.Followers[i].A_GiveInventory('HDFireDouse', 3);
				}
			}
			if (DistanceToPlayer <= HDCONST_ONEMETRE)
			{
				A_GiveInventory('HDFireDouse', 3);
				LinkedPlayer.A_GiveInventory('HDFireDouse', 3);
			}
		}

		// [Ace] Enemy searching and weapon handling.
		if (Weapons.Size() > 0)
		{
			if (HasUsableWeapons() && (!SelWeapon || SelWeapon.Status == FollowerWeapon.WStatus_Idle) && (!bFRIENDLY || Stance == FStance_Aggressive))
			{
				AcquireTargets();
			}

			if (SelWeapon && SelWeapon.Status == FollowerWeapon.WStatus_Reloading)
			{
				if (SelWeapon.DoReloadSequence(WeaponTicker))
				{
					IdleWeapon();
				}
				else
				{
					ClearTarget(); // [Ace] Allow others to target the enemy if currently reloading.
					WeaponTicker++;
				}
			}
			else if (CheckTarget())
			{
				if ((!SelWeapon || SelWeapon.Status == FollowerWeapon.WStatus_Idle) && ReactionTime-- <= 0)
				{
					SelectBestWeapon(FTarget.Actor);
					if (SelWeapon && SelWeapon.Status == FollowerWeapon.WStatus_Idle)
					{
						bNOPAIN = true;
						SelWeapon.Status = FollowerWeapon.WStatus_Firing;
						SetStateLabel('See');
					}
					else
					{
						ClearTarget(); // [Ace] Allow others to target the enemy if suddenly out of weapons.
					}
				}
			}
			else
			{
				ClearTarget();
				ReactionTime = default.ReactionTime;
				if (SelWeapon && SelWeapon.Status == FollowerWeapon.WStatus_Idle)
				{
					if (hdf_debug & FDebug_Weapons)
					{
						Console.Printf("[%s] %s's %s was nulled. Was idling.", GetTimestamp(), GetTag(), SelWeapon.GetName());
					}
					SelWeapon = null;
				}
			}
		}

		// [Ace] Crouching. One day I will implement proper sprites for this. Today is not that day.
		if (bFRIENDLY && Status == FStatus_None && Order <= FOrder_Stay && PlayerIsCrouching() && !PlayerIsProne() && CanSeePlayer && DistanceToPlayer < HDCONST_ONEMETRE * 30 || pos.z + default.Height > ceilingz)
		{
			CrouchFactor = min(CrouchFactor + 0.14, 0.5);
		}
		else if (CrouchFactor > 0)
		{
			CrouchFactor = max(CrouchFactor - 0.10, 0);
		}

		scale.y = default.scale.y * (1.0 - CrouchFactor);
		A_SetSize(-1, default.Height * (1.0 - CrouchFactor));

		double goalDist2 = TGoal ? Distance2D(TGoal) : 0;

		// [Ace] Movement. Always travel to goals if there are any.
		if (Status == FStatus_None && (!SelWeapon || SelWeapon.Status != FollowerWeapon.WStatus_Firing) && TGoal && goalDist2 < MaxGoalRange)
		{
			double distDiff = max(goalDist2 - GetMinRange(TGoal, distDiv), 0);
			double targetSpeed = min(0.2 + (distDiff * 0.5) / MaxMovementSpeed, MaxMovementSpeed);
			if (MovementSpeed < targetSpeed)
			{
				MovementSpeed = min(MovementSpeed + 0.5, targetSpeed);
			}
			else if (MovementSpeed > targetSpeed)
			{
				MovementSpeed = max(targetSpeed, MovementSpeed - 0.5);
			}

			if (MovementSpeed > MaxMovementSpeed * 0.25 && tics == 8)
			{
				tics = 4;
			}
			if (MovementSpeed > MaxMovementSpeed * 0.75 && tics == 4)
			{
				tics = 3;
			}

			if (distDiff < 0.25)
			{
				A_ChangeVelocity(0, 0, vel.z, CVF_REPLACE);
				ClearGoal(true);
			}
			else
			{
				// [Ace] Same as the corner thing from above but does it if the follower is moving.
				A_Face(TGoal, 0, 0);

				int sideVel = 0;
				if (!(blockedSides & BSide_Left) && blockedSides & BSide_Right)
				{
					sideVel = 1;
				}
				if (blockedSides & BSide_Left && !(blockedSides & BSide_Right))
				{
					sideVel = -1;
				}
				A_ChangeVelocity(MovementSpeed, MovementSpeed * sideVel, vel.z, CVF_RELATIVE | CVF_REPLACE);

				// [Ace] Ascend/descend ladders.
				bool nearLadder = CheckProximity('hdladdertop', radius + 32) || CheckProximity('hdladderbottom', radius + 32);
				MaxDropOffHeight = default.MaxDropOffHeight + (NearLadder ? LADDER_MAX : 0);
				bFLOORHUGGER = nearLadder;

				// [Ace] There is a gap that can be crossed. If the player can do it, so can the follower.
				bNODROPOFF = !(goalDist2 < HDCONST_ONEMETRE * 10 && (pos.z > floorz || abs(TGoal.floorz - floorz) < default.MaxDropOffHeight) && abs(TGoal.pos.z - pos.z) < default.MaxDropOffHeight);

				// [Ace] Stop trying to reach goal shortly after losing LOS.
				if (!CheckSight(TGoal, SF_ALL) && ++NoSightTics == 50)
				{
					NoSightTics = 0;
					ClearGoal(true);
				}

				// [Ace] Only play idle comments if moving.
				if (CommentTimer <= 0)
				{
					PlayIdleComment();
				}
			}
		}

		Moving = vel.xy.length() > 0.01;

		if (!Moving && Order == FOrder_GoThere)
		{
			Order = FOrder_Stay;
		}
	}

	private void TryActivateLine()
	{
		FLineTraceData data;
		LineTrace(angle, 40, 0, 0, height - 8, data: data);
		if (data.HitType == FLineTraceData.TRACE_HitWall)
		{
			if (ActivateAsPlayer)
			{
				data.HitLine.Activate(LinkedPlayer, 0, SPAC_Use);
			}
			else
			{
				data.HitLine.Activate(self, 0, SPAC_Use);
			}
		}
		ActivateAsPlayer = false;
	}

	// --------------------------------------------------
	// INFORMATION
	// --------------------------------------------------

	private Actor GetNextInChain()
	{
		int findex = Handler.Followers.Find(self);
		for (int i = findex - 1; i >= 0; --i)
		{
			HDFollower flw = Handler.Followers[i];
			if (!flw)
			{
				Handler.Followers.Delete(i);
				continue;
			}
			if (flw.Status != FStatus_InactiveOrDead && flw.bFRIENDLY && flw.Order == FOrder_Follow && flw.LinkedPlayer == LinkedPlayer && Distance3D(flw) < MaxFollowRange)
			{
				return Handler.Followers[i];
			}
		}
		return LinkedPlayer;
	}

	bool IsInIdleState()
	{
		return InStateSequence(CurState, FindState('Idle'));
	}

	bool IsInMovingState()
	{
		return InStateSequence(CurState, FindState('Moving'));
	}

	bool IsSteppingOnSomeone()
	{
		if (bOnMobj && !TGoal)
		{
			if (Distance2D(LinkedPlayer) <= LinkedPlayer.Radius + Radius)
			{
				return true;
			}

			for (int i = 0; i < Handler.Followers.Size(); ++i)
			{
				if (Distance2D(Handler.Followers[i]) <= Handler.Followers[i].Radius + Radius)
				{
					return true;
				}
			}
		}

		return false;
	}

	int GetBlockedSides()
	{
		int sides = 0;
		FLineTraceData data;

		LineTrace(angle - 25, radius + 7, 0, TRF_SOLIDACTORS, height - 6, 0, -radius, data);
		if (data.HitLine || data.HitActor)
		{
			sides |= BSide_Left;
		}
		LineTrace(angle + 25, radius + 7, 0, TRF_SOLIDACTORS, height - 6, 0, radius, data);
		if (data.HitLine || data.HitActor)
		{
			sides |= BSide_Right;
		}
		
		return sides;
	}

	double GetMinRange(Actor trgt, double distDiv)
	{
		if (!trgt)
		{
			return 0;
		}

		if (Order == FOrder_ComeAndHelp && (PNeedsMedic || PNeedsHelp))
		{
			return trgt.Radius + Radius;
		}

		if (trgt is 'TargetGoal')
		{
			return HDCONST_ONEMETRE * 0.3;
		}
		
		return (HDCONST_ONEMETRE * 2) / distDiv;
	}

	// --------------------------------------------------
	// CONSTANTS
	// --------------------------------------------------

	const MaxFollowRange = HDCONST_ONEMETRE * 25.0;
	const MaxWaitRange = HDCONST_ONEMETRE * 12;
	const MaxSearchRange = 24; // [Ace] This is for the LookFor* stuff.

	const SF_ALL = SF_IGNOREVISIBILITY | SF_SEEPASTSHOOTABLELINES | SF_SEEPASTBLOCKEVERYTHING | SF_IGNOREWATERBOUNDARY;
	const SF_NOINVISIBLE = SF_ALL & ~SF_IGNOREVISIBILITY;

	// --------------------------------------------------
	// VARIABLES
	// --------------------------------------------------

	HDPlayerPawn LinkedPlayer;
	protected FollowerTarget FTarget;
	private Actor TGoal;

	private bool Moving;
	FStatus Status;
	FOrder Order;
	FStance Stance;
	private FOrder PrevOrder;

	private vector3 LastKnownPos;

	private int CommentTimer;
	private bool ActivateAsPlayer;
	private int NoSightTics; // [Ace] Used with CheckSight.

	private bool CanSeePlayer;
	private double DistanceToPlayer;
	private bool PNeedsHelp;
	private bool PNeedsMedic;
	private double CrouchFactor;
}